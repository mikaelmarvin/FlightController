*******************************************************
* Taiwan Semiconductor
* TSM260P02CX
* P-Channel 20V
* Date: 2023-12-29
* Version: 1
*******************************************************
.SUBCKT TSM260P02CX D G S
m1 3 gx s s pmos W= 7396770u L=0.25u
m2 s gx s d nmos W= 7396770u L=0.069767u
r1 d 3 14.153E-3 tc=2.7916E-03,1.6972E-06
cgs gx s 597.6e-12
cgd gx d 89.3e-12
rg g gy 1 
rtcv 100 s 1e6 tc=-0.55e-4,1.0e-6
etcv gy gx 100 200 1
itcv s 100 1u
vtcv 200 s 1
dbd d s dbd 
****************************************************************
.model PMOS pmos (
+ LEVEL = 3           TOX = 3E-8           VMAX=3.25e5
+ KP = 2.469e-6     NSUB = 9.8832E15       KAPPA=0.006     VTO=-0.88
+ NFS = 6.3994E11       LD = 0              IS = 0                         
+ TPG = -1              )               
***************************************************************
.model NMOS nmos (
+ LEVEL = 3            TOX = 3E-8          NSUB = 3.3e16                    
+ IS = 0               TPG = -1              )                    
****************************************************************
.model DBD d (
+ FC = 0.1             TT = 22.7E-9                     T_MEASURED = 25   
+ TBV1=4.2502E-04      TBV2=-3.6133E-07
+ BV = 22.58           RS = 19.079E-3                 N =.73478                
+ IS = 1.0000E-12      EG = 1.16                      XTI = -7.7                       
+ TRS1 = -0.0002       CJO = 312.67E-12               VJ =.49976
+ M = .53502           IKF=53.168E-3)                
****************************************************************

.ENDS          